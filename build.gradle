apply from: "$rootDir/buildsystem/dependencies.gradle"

buildscript {
    apply from: "$rootDir/buildsystem/plugins.gradle"

    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$pluginAndroidTools"
    }
}

subprojects {
    buildscript {
        apply from: "$rootDir/buildsystem/plugins.gradle"

        repositories {
            jcenter()
        }
        dependencies {
            classpath "com.dicedmelon.gradle:jacoco-android:$pluginJacocoAndroidVersion"
            classpath "org.jacoco:org.jacoco.core:$pluginJacocoVersion"
        }
    }
}

allprojects {
    ext {
        androidApplicationId = 'sample.template'
        androidVersionCode = 1
        androidVersionName = "1.0"
        testInstrumentationRunner = "sample.template.test.MockTestRunner"
        testApplicationId = 'sample.template.test'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    apply from: "$rootDir/buildsystem/plugins.gradle"

    afterEvaluate {
        def hasAndroidJacocoPlugin = project.plugins.hasPlugin('jacoco-android')

        if (hasAndroidJacocoPlugin) {
            jacocoAndroidUnitTestReport {
                excludes += [
                        '**/*ViewBinder.class',
                        '**/*$Parcelable*.*',
                        '**/*_Factory.*',
                        '**/*$*$*.*',
                        '**/*$*.*'
                ]
            }
            jacoco {
                toolVersion = pluginJacocoVersion
            }
        }
    }
}
